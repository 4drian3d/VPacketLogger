plugins {
    java
    alias(libs.plugins.runvelocity)
    alias(libs.plugins.blossom)
    alias(libs.plugins.shadow)
}

repositories {
    maven("https://repo.papermc.io/repository/maven-public/")
    maven("https://maven.elytrium.net/repo/")
}

dependencies {
    compileOnly(libs.velocity.api)
    compileOnly(libs.velocity.proxy)
    annotationProcessor(libs.velocity.api)
    compileOnly(libs.vpacketevents)
    implementation(libs.hexlogger)
}

tasks {
    build {
        dependsOn(shadowJar)
    }
    compileJava {
        options.encoding = Charsets.UTF_8.name()
        options.release.set(17)
    }
    clean {
        // Deletes the directory that is generated by the runVelocity plugin
        delete("run")
    }
    runVelocity {
        velocityVersion(libs.versions.velocity.get())
    }
    shadowJar {
        relocate("io.github._4drian3d.velocityhexlogger", "io.github._4drian3d.vpacketlogger.velocityhexlogger")
        relocate("net.kyori.adventure.text.logger.slf4j", "io.github._4drian3d.vpacketlogger.component.logger")
    }
}

blossom {
    replaceTokenIn("src/main/java/io/github/_4drian3d/vpacketlogger/Constants.java")
    replaceToken("{version}", project.version)
}

java.toolchain.languageVersion.set(JavaLanguageVersion.of(17))
